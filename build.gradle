plugins {
    id "org.springframework.boot" version "2.4.3"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java"
    id "jacoco"
    id "com.diffplug.spotless" version "5.11.0"
}

group = "org.miracum.etl"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "11"

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springCloudVersion', "Hoxton.SR6")
}


repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator:2.4.3"
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:2.4.3"
    implementation "org.springframework.boot:spring-boot-starter-web:2.4.3"
    implementation "io.micrometer:micrometer-registry-prometheus:1.6.5"
    implementation "io.micrometer:micrometer-core:1.6.5"
    implementation "org.springframework.retry:spring-retry:1.3.1"

    implementation "org.springframework.cloud:spring-cloud-stream"
    implementation "org.springframework.cloud:spring-cloud-stream-binder-kafka"
    implementation "org.springframework.kafka:spring-kafka"
    implementation "org.miracum:kafka-fhir-serializer:1.0.1"

    implementation "net.logstash.logback:logstash-logback-encoder:6.6"

    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:5.3.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:5.3.0"
    implementation "ca.uhn.hapi.fhir:org.hl7.fhir.r4:5.3.5"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.3.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:5.3.0"

    implementation "io.opentracing.contrib:opentracing-spring-web-starter:4.1.0"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-okhttp3:3.0.0"
    implementation "io.jaegertracing:jaeger-client:1.5.0"

    developmentOnly "org.springframework.boot:spring-boot-devtools:2.4.3"
    runtimeOnly "org.postgresql:postgresql:42.2.19"
    runtimeOnly "com.h2database:h2:1.4.200"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:2.4.3"
    testImplementation("org.springframework.boot:spring-boot-starter-test:2.4.3") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacoco-reports")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        csv.destination file("${buildDir}/jacoco/coverage.csv")
        html.destination file("${buildDir}/jacoco/html")
    }
}

bootJar {
    layered()
}

spotless {
    java {
        importOrder() // standard import order
        // You probably want an empty string at the end - all of the
        // imports you didn't specify explicitly will go there.

        removeUnusedImports()

        googleJavaFormat() // has its own section below
    }
}
